# Copyright (C) The DDC development team, see COPYRIGHT.md file
#
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

# Workaround for rocm <6 setting a cmake_minimum_required <3.12.
# When redefining `find_dependency` we get a better chance
# to use the NEW policy for CMP0074.
macro(ddc_find_dependency)
   include(CMakeFindDependencyMacro)
   find_dependency(${ARGN})
endmacro()

set(DDC_BUILD_DOUBLE_PRECISION @DDC_BUILD_DOUBLE_PRECISION@)

ddc_find_dependency(Kokkos 4.4...4.5)

if(@DDC_BUILD_KERNELS_FFT@)
   ddc_find_dependency(KokkosFFT 0.2.1...<1)
endif()

if(@DDC_BUILD_KERNELS_SPLINES@)
   ddc_find_dependency(Ginkgo 1.8.0 EXACT)
   # DDC installs a FindLAPACKE.cmake file.
   # We choose to rely on it by prepending to CMAKE_MODULE_PATH
   # only the time of calling ddc_find_dependency.
   list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
   ddc_find_dependency(LAPACKE)
   list(POP_FRONT CMAKE_MODULE_PATH)
   # fork https://github.com/yasahi-hpc/kokkos-kernels
   # on branch develop-spline-kernels-v2
   ddc_find_dependency(KokkosKernels)
endif()

if(@DDC_BUILD_PDI_WRAPPER@)
   ddc_find_dependency(PDI 1.6...<2 COMPONENTS C)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/DDCTargets.cmake)

foreach(target core fft pdi splines)
   if(TARGET DDC::impl::ddc_${target})
      add_library(DDC::${target} ALIAS DDC::impl::ddc_${target})
   endif()
endforeach()

foreach(target_component fft pdi splines)
   if(TARGET DDC::impl::ddc_${target_component})
      set(DDC_${target_component}_FOUND TRUE)
   endif()
endforeach()

check_required_components(DDC)
