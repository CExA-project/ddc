# Copyright (C) The DDC development team, see COPYRIGHT.md file
#
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

set(DDC_BUILD_DOUBLE_PRECISION @DDC_BUILD_DOUBLE_PRECISION@)
set(DDC_BUILD_KERNELS_FFT @DDC_BUILD_KERNELS_FFT@)
set(DDC_BUILD_KERNELS_SPLINES @DDC_BUILD_KERNELS_SPLINES@)
set(DDC_BUILD_PDI_WRAPPER @DDC_BUILD_PDI_WRAPPER@)

find_package(Kokkos 4.4...4.5)

if(@DDC_BUILD_KERNELS_FFT@)
   find_dependency(KokkosFFT 0.2.1...<1)
endif()

if(@DDC_BUILD_KERNELS_SPLINES@)
   find_dependency(Ginkgo 1.8.0 EXACT)
   # DDC installs a FindLAPACKE.cmake file.
   # We choose to rely on it by prepending to CMAKE_MODULE_PATH
   # only the time of calling find_dependency.
   list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
   find_dependency(LAPACKE)
   list(POP_FRONT CMAKE_MODULE_PATH)
   find_dependency(KokkosKernels)
endif()

if(@DDC_BUILD_PDI_WRAPPER@)
   find_dependency(PDI COMPONENTS C)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/DDCTargets.cmake)

foreach(target fft;pdi;splines)
   if(TARGET DDC::ddc_${target})
      add_library(DDC::${target} ALIAS DDC::ddc_${target})
   endif()
endforeach()

check_required_components(DDC)
