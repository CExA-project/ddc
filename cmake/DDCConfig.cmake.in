# Copyright (C) The DDC development team, see COPYRIGHT.md file
#
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

# This macro extends the cmake macro `check_required_components`.
# It checks if all required components for a package are found and
# sets a custom NOT_FOUND_MESSAGE listing missing required components.
macro(ddc_check_required_components _NAME)
    set(REQUIRED_COMPONENTS_NOT_FOUND "")
    foreach(comp ${${_NAME}_FIND_COMPONENTS})
        if(NOT ${_NAME}_${comp}_FOUND)
            if(${_NAME}_FIND_REQUIRED_${comp})
                list(APPEND REQUIRED_COMPONENTS_NOT_FOUND "${comp}")
                set(${_NAME}_FOUND FALSE)
            endif()
        endif()
    endforeach()

    if(REQUIRED_COMPONENTS_NOT_FOUND)
        set(${_NAME}_NOT_FOUND_MESSAGE "Component(s) not found: ${REQUIRED_COMPONENTS_NOT_FOUND}")
    endif()
    unset(REQUIRED_COMPONENTS_NOT_FOUND)
endmacro()

set(DDC_BUILD_DOUBLE_PRECISION @DDC_BUILD_DOUBLE_PRECISION@)

include(${CMAKE_CURRENT_LIST_DIR}/DDCCoreTargets.cmake)

if(@DDC_BUILD_KERNELS_FFT@ AND ("fft" IN_LIST DDC_FIND_COMPONENTS))
    include(${CMAKE_CURRENT_LIST_DIR}/DDCFftTargets.cmake)
    set(DDC_fft_FOUND TRUE)
endif()

if(@DDC_BUILD_KERNELS_SPLINES@ AND ("splines" IN_LIST DDC_FIND_COMPONENTS))
    # DDC installs a FindLAPACKE.cmake file.
    # We choose to rely on it by prepending to CMAKE_MODULE_PATH
    # only the time of calling ddc_find_dependency.
    list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
    include(${CMAKE_CURRENT_LIST_DIR}/DDCSplinesTargets.cmake)
    list(POP_FRONT CMAKE_MODULE_PATH)
    set(DDC_splines_FOUND TRUE)
endif()

if(@DDC_BUILD_PDI_WRAPPER@ AND ("pdi" IN_LIST DDC_FIND_COMPONENTS))
    include(${CMAKE_CURRENT_LIST_DIR}/DDCPdiTargets.cmake)
    set(DDC_pdi_FOUND TRUE)
endif()

ddc_check_required_components(DDC)
