# Copyright (C) The DDC development team, see COPYRIGHT.md file
#
# SPDX-License-Identifier: MIT

@PACKAGE_INIT@

# Workaround for rocm <6 setting a cmake_minimum_required <3.12.
# When redefining `find_dependency` we get a better chance
# to use the NEW policy for CMP0074.
macro(ddc_find_dependency)
   include(CMakeFindDependencyMacro)
   find_dependency(${ARGN})
endmacro()

set(DDC_BUILD_DOUBLE_PRECISION @DDC_BUILD_DOUBLE_PRECISION@)

ddc_find_dependency(Kokkos @Kokkos_VERSION_MAJOR@.@Kokkos_VERSION_MINOR@.@Kokkos_VERSION_PATCH@)

include(${CMAKE_CURRENT_LIST_DIR}/DDCCheckRequiredKokkosOptions.cmake)
ddc_check_required_kokkos_options()

if(@DDC_BUILD_KERNELS_FFT@)
   ddc_find_dependency(KokkosFFT @KokkosFFT_VERSION_MAJOR@.@KokkosFFT_VERSION_MINOR@.@KokkosFFT_VERSION_PATCH@)
endif()

if(@DDC_BUILD_KERNELS_SPLINES@)
   ddc_find_dependency(Ginkgo @Ginkgo_VERSION_MAJOR@.@Ginkgo_VERSION_MINOR@.@Ginkgo_VERSION_PATCH@)
   # DDC installs a FindLAPACKE.cmake file.
   # We choose to rely on it by prepending to CMAKE_MODULE_PATH
   # only the time of calling ddc_find_dependency.
   list(PREPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
   ddc_find_dependency(LAPACKE)
   list(POP_FRONT CMAKE_MODULE_PATH)
   ddc_find_dependency(KokkosKernels @KokkosKernels_VERSION_MAJOR@.@KokkosKernels_VERSION_MINOR@.@KokkosKernels_VERSION_PATCH@)
endif()

if(@DDC_BUILD_PDI_WRAPPER@)
   ddc_find_dependency(PDI @PDI_VERSION_MAJOR@.@PDI_VERSION_MINOR@.@PDI_VERSION_PATCH@ COMPONENTS C)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/DDCTargets.cmake)

# We always define public alias targets when impl targets are available.
foreach(target core fft pdi splines)
   if((NOT TARGET DDC::${target}) AND (TARGET DDC::impl::ddc_${target}))
      add_library(DDC::${target} ALIAS DDC::impl::ddc_${target})
   endif()
endforeach()

# Components are found if the public targets exist
foreach(component ${DDC_FIND_COMPONENTS})
   if(TARGET DDC::${component})
      set(DDC_${component}_FOUND TRUE)
   endif()
endforeach()

check_required_components(DDC)
